apply plugin: 'groovy'

repositories {
    jcenter()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

dependencies {
    // main dependencies
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'
    implementation group: 'com.cloudbees', name: 'groovy-cps', version: '1.32'
    implementation group: 'org.jenkins-ci.main', name: 'jenkins-core', version: '2.277'
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    // @grab dependencies
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.23'

    // test dependencies
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.lesfurets', name: 'jenkins-pipeline-unit', version: '1.1'
}

task cleanDist(type: Delete) {
    delete 'dist'
}

task copyDist(type: Copy) {
    from('./') {
        include 'resources/**/*'
        include 'test-vars/**/*'
    }
    into 'dist/jenkins-shared-library@master'
    doLast {
        file('dist/jenkins-shared-library@master/test-vars')
            .renameTo(file('dist/jenkins-shared-library@master/vars'))
    }
}

test {
    dependsOn cleanDist
    dependsOn copyDist
    systemProperty 'groovy.grape.enable', 'false'
    testLogging.showStandardStreams = true
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

test.outputs.upToDateWhen {
    false
}

compileGroovy {
    groovyOptions.forkOptions.jvmArgs = ['-Dgroovy.grape.enable=false']
}

compileTestGroovy {
    groovyOptions.forkOptions.jvmArgs = ['-Dgroovy.grape.enable=false']
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
    }

    test {
        groovy {
            srcDirs = ['test']
        }
    }

    resources {
        groovy {
            srcDirs = ['resources', 'test-resources']
        }
    }
}